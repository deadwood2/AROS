dnl Process this file with autoconf to produce a configure script.

AC_INIT([MetaMake],[0.8])
AC_PREREQ([2.71])
AC_CONFIG_AUX_DIR([.])
AC_CONFIG_SRCDIR([mmake.c])

AC_CANONICAL_HOST

dnl Determine correct pwd command
case "$host_os" in
  mingw* ) PWDCMD="pwd -W" ;;
  * )      PWDCMD="pwd" ;;
esac

# Remove -g from CFLAGS early, before any compile tests run
AS_IF([test "x$CFLAGS" != x],
      [CFLAGS=`echo $CFLAGS | sed -e 's/-g//g'`])

dnl Determine current build path
AC_MSG_CHECKING([Building MetaMake in])
METAMAKE_BUILDDIR=`${PWDCMD}`
AC_MSG_RESULT([$METAMAKE_BUILDDIR])

dnl Determine source path
AC_MSG_CHECKING([MetaMake source in])
srcpwd=`cd "$srcdir" ; ${PWDCMD}`
METAMAKE_SRCDIR=$srcpwd
AC_MSG_RESULT([$METAMAKE_SRCDIR])

dnl Setup optional objfile output directory
AC_MSG_CHECKING([where to build objfiles])
AC_ARG_WITH([objfiledir],
  [AS_HELP_STRING([--with-objfiledir=DIR], [Build object files in DIR])],
  [with_objfiles="$withval"],
  [with_objfiles="sourcedir"]
)
if test "$with_objfiles" = "sourcedir"; then
  mmake_objdir="${srcpwd}/obj"
else
  mmake_objdir="$with_objfiles"
fi
AC_MSG_RESULT([$with_objfiles])

dnl Set build environment
AC_SUBST([host_os])
AC_SUBST([mmake_objdir])
AC_SUBST([METAMAKE_BUILDDIR])
AC_SUBST([METAMAKE_SRCDIR])

dnl Check for programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MKDIR_P

dnl Check for header files
AC_HEADER_DIRENT
AC_CHECK_HEADERS([stdio.h stdarg.h string.h unistd.h sys/stat.h sys/types.h netinet/in.h])

dnl Check for types
AC_C_CONST
AC_TYPE_SIZE_T

dnl Check for functions
AC_CHECK_FUNCS([vprintf getcwd strdup strndup strerror])

dnl Output config headers and files
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
